
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбменДаннымиСервер.ФормаНастройкиУзлаБазыКорреспондентаПриСозданииНаСервере(
		ЭтотОбъект, Метаданные.ПланыОбмена.ОбменУправлениеПредприятиемКлиентЭДО.Имя);
	
	Элементы.УчастникиЭДО.Доступность = ИспользоватьОтборПоУчастникамЭДО;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьОтборПоУчастникамЭДОПриИзменении(Элемент)
	
	Элементы.УчастникиЭДО.Доступность = ИспользоватьОтборПоУчастникамЭДО;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУчастникиЭДО

&НаКлиенте
Процедура УчастникиЭДООрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыВыбора = Новый Структура("ВыборГруппИЭлементов", Элемент.ВыборГруппИЭлементов);
	
	ОбменДаннымиКлиент.ОбработчикВыбораЭлементовБазыКорреспондентаНачалоВыбора("Организация", "Справочник.Организации", 
		Элемент, СтандартнаяОбработка, ПараметрыВнешнегоСоединения, ПараметрыВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиЭДООрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.УчастникиЭДО.ТекущиеДанные;
	ТекущиеДанные.Организация = ВыбранноеЗначение.Представление;
	ТекущиеДанные.Организация_Ключ = ВыбранноеЗначение.Идентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиЭДОКонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыВыбора = Новый Структура("ВыборГруппИЭлементов", Элемент.ВыборГруппИЭлементов);
	
	ОбменДаннымиКлиент.ОбработчикВыбораЭлементовБазыКорреспондентаНачалоВыбора("Контрагент", "Справочник.Контрагенты", 
		Элемент, СтандартнаяОбработка, ПараметрыВнешнегоСоединения, ПараметрыВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиЭДОКонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.УчастникиЭДО.ТекущиеДанные;
	ТекущиеДанные.Контрагент = ВыбранноеЗначение.Представление;
	ТекущиеДанные.Контрагент_Ключ = ВыбранноеЗначение.Идентификатор;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если Не ИспользоватьОтборПоУчастникамЭДО И УчастникиЭДО.Количество() <> 0 Тогда
		УчастникиЭДО.Очистить();
	ИначеЕсли УчастникиЭДО.Количество() = 0 И ИспользоватьОтборПоУчастникамЭДО Тогда
		ИспользоватьОтборПоУчастникамЭДО = Ложь;
	КонецЕсли;
	
	// Сохранение настроек
	ОбменДаннымиКлиент.ФормаНастройкиУзлаКомандаЗакрытьФорму(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
