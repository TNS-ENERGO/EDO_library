#Область ПрограммныйИнтерфейс

// См. РаботаСКонтрагентамиПереопределяемый.СформироватьОписаниеДокументовСоСчетомФактуройВПодвале.
Процедура СформироватьОписаниеДокументовСоСчетомФактуройВПодвале(Форма, Описание) Экспорт
	
	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	Если Форма.РеквизитыПроверкиКонтрагентов.ЭтоДокументСоСчетомФактуройВПодвале Тогда
		
		СчетФактура = ПроверкаКонтрагентовКлиентСервер.СчетФактура(Форма);
		
		Если ЗначениеЗаполнено(СчетФактура) Тогда
			
			ПроверкаКонтрагентов.ДополнитьОписание(
				Описание,
				СчетФактура, 
				Форма.Элементы.СчетФактураПредставление,
				Неопределено);
			
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

// См. РаботаСКонтрагентамиПереопределяемый.СформироватьОписаниеДокументовСКонтрагентомВШапке.
Процедура СформироватьОписаниеДокументовСКонтрагентомВШапке(Форма, Описание) Экспорт
	
	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	// Заполнение данных по контрагенту в шапке.
	Если Форма.РеквизитыПроверкиКонтрагентов.ЭтоДокументСКонтрагентомВШапке Тогда
		
		ПроверкаКонтрагентов.ДополнитьОписание(
			Описание, 
			ДокументСсылка, 
			Форма.Элементы.Контрагент,
			ДокументОбъект.Контрагент,
			Форма.Элементы.ГруппаКонтрагент);
			
	КонецЕсли;
	
КонецПроцедуры

// См. РаботаСКонтрагентамиПереопределяемый.СформироватьОписаниеСчетовФактур.
Процедура СформироватьОписаниеСчетовФактур(Форма, Описание) Экспорт
	
	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	// Заполнение данных для счета-фактуры.
	Если Форма.РеквизитыПроверкиКонтрагентов.ЭтоСчетФактура Тогда
		
		Ссылка 			= ДокументСсылка;
		ТипДокумента	= ТипЗнч(Ссылка);
		
		// Контрагент
		ПроверкаКонтрагентов.ДополнитьОписание(
			Описание, 
			Ссылка,
			Форма.Элементы.Контрагент,
			ДокументОбъект.Контрагент,
			Форма.Элементы.ГруппаКонтрагент);
		
	КонецЕсли;
	
КонецПроцедуры

// См. РаботаСКонтрагентамиПереопределяемый.СформироватьОписаниеСчетовФактур.
Процедура ПолучитьИменаРеквизитовКонтрагентовВСчетеФактуре(Тип, ИменаРеквизитов) Экспорт
	
	ИменаРеквизитов.Добавить("Контрагент");
	
КонецПроцедуры

// См. РаботаСКонтрагентамиПереопределяемый.ОпределитьДатуВДокументе.
Процедура ОпределитьДатуВДокументе(ДокументОбъект, Дата) Экспорт
	
	Дата = НачалоДня(ДокументОбъект.Дата);
	
КонецПроцедуры

// См. РаботаСКонтрагентамиПереопределяемый.ОпределитьДатуВСчетеФактуре.
Процедура ОпределитьДатуВСчетеФактуре(СчетФактураОбъект, Дата) Экспорт
	
	Дата = НачалоДня(СчетФактураОбъект.Дата);
	
КонецПроцедуры

// См. РаботаСКонтрагентамиПереопределяемый.ПриОпределенииСвойствСправочниковКонтрагентов.
Процедура ПриОпределенииСвойствСправочниковКонтрагентов(СвойстваСправочников) Экспорт
	
	НовСтрока = СвойстваСправочников.Добавить();
	НовСтрока.Имя = "Контрагенты";
	НовСтрока.Иерархический = Истина;
	НовСтрока.РеквизитИНН = "ИНН";
	НовСтрока.РеквизитКПП = "КПП";
	
КонецПроцедуры

// См. РаботаСКонтрагентамиПереопределяемый.ПриОпределенииВидаКонтрагента.
Процедура ПриОпределенииВидаКонтрагента(СтандартнаяОбработка, ЭтоЮрЛицо, ДанныеКонтрагента) Экспорт
	
	Если ДанныеКонтрагента.ДополнительныеПараметры.Свойство("ЭтоЮридическоеЛицо") Тогда
		ЭтоЮрЛицо = ДанныеКонтрагента.ДополнительныеПараметры.ЭтоЮридическоеЛицо;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// См. РаботаСКонтрагентамиПереопределяемый.ДополнитьРеквизитыФормыКонтрагент.
Процедура ДополнитьРеквизитыФормыКонтрагент(Форма, НовыеРеквизиты) Экспорт
	
	НовыеРеквизиты.ЭтоИностранныйКонтрагент =
		Форма.Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент");
		
	Если НовыеРеквизиты.ЭтоИностранныйКонтрагент Тогда
		НовыеРеквизиты.ЭтоЮридическоеЛицо = Истина;
	Иначе
		НовыеРеквизиты.ЭтоЮридическоеЛицо =
			Форма.Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
