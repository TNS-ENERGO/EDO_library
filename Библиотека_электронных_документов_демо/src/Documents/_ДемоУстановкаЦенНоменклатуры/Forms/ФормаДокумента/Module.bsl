
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыгрузитьВФайл(Команда)
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ВыгрузитьВФайлЗавершить", ЭтотОбъект);
	ОткрытьФорму("Справочник.Организации.ФормаВыбора", ,,,,,ОбработчикОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыгрузитьВФайлЗавершить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АдресТаблицыЦен = ЦеныНоменклатуры();
	
	СтруктураЭД = Новый Структура;
	СтруктураЭД.Вставить("ВидЭД", ПредопределенноеЗначение("Перечисление.ВидыЭД.ПрайсЛист"));
	СтруктураЭД.Вставить("АдресТаблицыЦен", АдресТаблицыЦен);
	СтруктураЭД.Вставить("ДатаФормирования", Объект.Дата);
	СтруктураЭД.Вставить("ВладелецЭД", Результат);
	СтруктураЭД.Вставить("ТипЭлементаВерсииЭД", 
		ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ПервичныйЭД"));
	СтруктураЭД.Вставить("Организация", Результат);
	
	ПараметрыЭД = Новый Структура("ВыгрузитьВФайл", СтруктураЭД);
	
	ОбменСКонтрагентамиКлиент.СформироватьФайлБыстрогоОбмена(ПараметрыЭД);
	
КонецПроцедуры

&НаСервере
Функция ЦеныНоменклатуры()
	
	ЦеныНоменклатуры = ТаблицаЦен();
	
	ЦеныДокумента = РеквизитФормыВЗначение("Объект").Товары;
	Для Каждого ТекСтрока Из ЦеныДокумента Цикл
		НоваяСтрока = ЦеныНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		НоваяСтрока.ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекСтрока.Номенклатура,"ЕдиницаИзмерения");
	КонецЦикла;
	
	ВидЦены = РеквизитФормыВЗначение("Объект").ВидЦены;
	ЦеныНоменклатуры.ЗаполнитьЗначения(ВидЦены, "ТипЦены");
	
	АдресЦенНоменклатуры = ПоместитьВоВременноеХранилище(ЦеныНоменклатуры, Новый УникальныйИдентификатор);
	
	Возврат АдресЦенНоменклатуры;
	
КонецФункции

&НаСервере
Функция ТаблицаЦен()
	
	ТаблицаЦен = Новый ТаблицаЗначений;
	ТаблицаЦен.Колонки.Добавить("Номенклатура");
	ТаблицаЦен.Колонки.Добавить("Характеристика");
	ТаблицаЦен.Колонки.Добавить("ЕдиницаИзмерения");
	ТаблицаЦен.Колонки.Добавить("Количество");
	ТаблицаЦен.Колонки.Добавить("ТипЦены");
	ТаблицаЦен.Колонки.Добавить("Цена");
	
	Возврат ТаблицаЦен;
	
КонецФункции

#КонецОбласти