
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВидОперации          = Параметры.ВидОперации;
	Организация          = Параметры.Организация;
	Контрагент           = Параметры.Контрагент;
	ДоговорКонтрагента   = Параметры.ДоговорКонтрагента;
	СписокОснований      = Параметры.СписокОснований;
	СчетФактураОснование = Параметры.СчетФактураОснование;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокОснований

&НаКлиенте
Процедура СписокОснованийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(,Элемент.ТекущиеДанные.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОснованийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ДобавитьДокументыОснования();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)

	ПеренестиДанные();

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)

	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьДокументыОснования()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ВидОперации",          ВидОперации);
	ПараметрыФормы.Вставить("Организация",          Организация);
	ПараметрыФормы.Вставить("Контрагент",           Контрагент);
	ПараметрыФормы.Вставить("ДоговорКонтрагента",   ДоговорКонтрагента);
	ПараметрыФормы.Вставить("СписокОснований",      СписокОснований);
	ПараметрыФормы.Вставить("СчетФактураОснование", СчетФактураОснование);
	
	Оповещение = Новый ОписаниеОповещения("ДобавитьДокументыОснованияЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Документ.СчетФактураВыданный.Форма.ФормаПодбораОснованийСчетаФактуры",
		ПараметрыФормы, ЭтотОбъект, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДокументыОснованияЗавершение(Результат, Контекст) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Для Каждого Основание Из Результат Цикл
			Если СписокОснований.НайтиПоЗначению(Основание) = Неопределено Тогда
				СписокОснований.Добавить(Основание);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиДанные()
	
	Закрыть(СписокОснований.ВыгрузитьЗначения());
	
КонецПроцедуры

#КонецОбласти