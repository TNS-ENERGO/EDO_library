#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСуммуДокумента();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ВидЭД = "";
	НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
	СуммаДокумента = 0;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ВидЭД = Перечисления.ВидыЭД.ПрайсЛист Тогда
		
		Если НаправлениеЭД = Перечисления.НаправленияЭД.Входящий
			И ПроверяемыеРеквизиты.Найти("Организация") <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Организация"));
		КонецЕсли;
		
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Товары.Количество"));
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Товары.Сумма"));
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Валюта"));
		
	КонецЕсли;
	
	Если Не ВидЭД = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Товары.Покупатель"));
	КонецЕсли;
	
	Если Не ВидЭД = Перечисления.ВидыЭД.СчетНаОплату Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Товары.СтавкаНДС"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	//{{__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка._ДемоОперация") Тогда
		// Заполнение шапки
		Валюта = ДанныеЗаполнения.Валюта;
		ВидЭД = Перечисления.ВидыЭД.СчетНаОплату;
		Комментарий = ДанныеЗаполнения.Комментарий;
		Контрагент = ДанныеЗаполнения.Контрагент;
		НаправлениеЭД = ДанныеЗаполнения.НаправлениеЭД;
		Организация = ДанныеЗаполнения.Организация;
		Основание = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
		Для Каждого ТекСтрокаТовары Из ДанныеЗаполнения.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.ЕдиницаИзмерения = ТекСтрокаТовары.ЕдиницаИзмерения;
			НоваяСтрока.Количество = ТекСтрокаТовары.Количество;
			НоваяСтрока.Номенклатура = ТекСтрокаТовары.Номенклатура;
			НоваяСтрока.Покупатель = ТекСтрокаТовары.Покупатель;
			НоваяСтрока.Сумма = ТекСтрокаТовары.Сумма;
			НоваяСтрока.Характеристика = ТекСтрокаТовары.Характеристика;
			НоваяСтрока.Цена = ТекСтрокаТовары.Цена;
			НоваяСтрока.ДокументОснование = ДанныеЗаполнения.Ссылка;
		КонецЦикла;
	КонецЕсли;
	//}}__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьСуммуДокумента() Экспорт
	
	Если ВидЭД = Перечисления.ВидыЭД.ПрайсЛист Тогда
		СуммаДокумента = 0;
		Возврат;
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.СчетНаОплату Тогда
		СуммаДокумента = Товары.Итог("СуммаСНДС");
	Иначе 
		СуммаДокумента = Товары.Итог("Сумма");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
