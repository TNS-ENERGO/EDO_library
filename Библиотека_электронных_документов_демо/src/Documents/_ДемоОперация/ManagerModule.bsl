#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получение данных для формирования печатной формы заказа.
//
// Параметры:
//  ПараметрыПечати	 - ПараметрыМакетаТабличногоДокумента - параметры макета.
//  СсылкаНаОбъект	 - ДокументСсылка._ДемоЗаказПокупателя - ссылка на документ печати.
// 
// Возвращаемое значение:
//  Структура - структура данных для печати.
//
Функция ПолучитьДанныеДляПечатнойФормыЗаказ(ПараметрыПечати, СсылкаНаОбъект) Экспорт

	КолонкаКодов = "Код";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Номенклатура.Наименование КАК Наименование,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Характеристика.Код КАК КодХарактеристики,
	|	Товары.Характеристика.Наименование КАК НаименованиеХарактеристики,
	|	Товары.Номенклатура.Код КАК Артикул,
	|	Товары.Номенклатура.Код КАК КодТовара,
	|	ВЫРАЗИТЬ(Товары.ЕдиницаИзмерения.НаименованиеПолное КАК СТРОКА(3)) КАК Единица,
	|	Товары.ЕдиницаИзмерения.Код КАК БазоваяЕдиницаКод,
	|	НЕОПРЕДЕЛЕНО КАК ВидУпаковки,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК МассаНетто,
	|	Товары.Количество КАК МассаБрутто,
	|	Товары.Цена,
	|	Товары.Сумма КАК СуммаБезНДС,
	|	ЕСТЬNULL(Товары.Номенклатура.СтавкаНДС, ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)) КАК СтавкаНДС,
	|	Товары.Сумма * 18 / 100 КАК СуммаНДС,
	|	Товары.Сумма КАК СуммаСНДС,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(110)) КАК Ид,
	|	Товары.ЕдиницаИзмерения.Наименование КАК БазоваяЕдиницаНаименование,
	|	Товары.ЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	Товары.ЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	НЕОПРЕДЕЛЕНО КАК УпаковкаНаименование,
	|	1 КАК Коэффициент,
	|	НЕОПРЕДЕЛЕНО КАК УпаковкаКод,
	|	НЕОПРЕДЕЛЕНО КАК Упаковка,
	|	""2389473485348"" КАК ШтрихКод
	|ИЗ
	|	Документ._ДемоЗаказПокупателя.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В(&Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_ДемоЗаказПокупателя.Номер КАК Номер,
	|	_ДемоЗаказПокупателя.Дата КАК Дата,
	|	_ДемоЗаказПокупателя.Организация КАК Исполнитель,
	|	_ДемоЗаказПокупателя.Контрагент КАК Заказчик,
	|	_ДемоЗаказПокупателя.Контрагент КАК Грузополучатель,
	|	_ДемоЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	|	&ХозОперация КАК ХозОперация,
	|	&Роль КАК Роль,
	|	&Руководитель КАК Руководитель,
	|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
	|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	&ДолжностьГлавногоБухгалтера КАК ДолжностьГлавногоБухгалтера,
	|	&Кладовщик КАК Кладовщик,
	|	&ДолжностьКладовщика КАК ДолжностьКладовщика,
	|	ИСТИНА КАК ЦенаВключаетНДС,
	|	_ДемоЗаказПокупателя.Валюта.Код КАК ВалютаКод,
	|	1 КАК Курс
	|ИЗ
	|	Документ._ДемоЗаказПокупателя КАК _ДемоЗаказПокупателя
	|ГДЕ
	|	_ДемоЗаказПокупателя.Ссылка В(&Ссылка)";
	
	Запрос.УстановитьПараметр("Ссылка",                      СсылкаНаОбъект);
	Запрос.УстановитьПараметр("ХозОперация",                 НСтр("ru = 'Заказ товара'"));
	Запрос.УстановитьПараметр("Роль",                        НСтр("ru = 'Продавец'"));
	Запрос.УстановитьПараметр("Руководитель",                НСтр("ru = 'Козлевич Адам Коземирович'"));
	Запрос.УстановитьПараметр("ДолжностьРуководителя",       НСтр("ru = 'Директор'"));
	Запрос.УстановитьПараметр("ГлавныйБухгалтер",            НСтр("ru = 'Синицкая Зося Викторовна'"));
	Запрос.УстановитьПараметр("ДолжностьГлавногоБухгалтера", НСтр("ru = 'Бухгалтер'"));
	Запрос.УстановитьПараметр("Кладовщик",                   НСтр("ru = 'Гайзенкамф Светлана Петровна'"));
	Запрос.УстановитьПараметр("ДолжностьКладовщика",         НСтр("ru = 'Кладовщик'"));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоТабличнойЧасти = МассивРезультатов[0];
	РезультатПоШапке = МассивРезультатов[1];
	
	СтруктураДанныхДляПечати = Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

#КонецОбласти

#КонецЕсли
