#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Регистрирует данные для обработчика обновления
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	_ДемоУдалитьБанковскиеСчета.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник._ДемоУдалитьБанковскиеСчета КАК _ДемоУдалитьБанковскиеСчета";
	
	МассивСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, МассивСсылок);
	
КонецПроцедуры

// Обработчик обновления.
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(
		Параметры.Очередь, "Справочник._ДемоУдалитьБанковскиеСчета");
	
	Пока Выборка.Следующий() Цикл
		УдалитьБанковскийСчет = Выборка.Ссылка.ПолучитьОбъект();
		БанковскийСчет = Справочники.БанковскиеСчета.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(БанковскийСчет, УдалитьБанковскийСчет,,"Родитель");
		БанковскийСчет.ОбменДанными.Загрузка = Истина;
		БанковскийСчет.Записать();
		ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь,
		"Справочник._ДемоУдалитьБанковскиеСчета");
	
КонецПроцедуры

// См. ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы.
Функция ДанныеОбновленыНаНовуюВерсиюПрограммы(МетаданныеИОтбор) Экспорт
	
	Если МетаданныеИОтбор.ПолноеИмя = "Справочник._ДемоУдалитьБанковскиеСчета" Тогда
		Возврат ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы(МетаданныеИОтбор);
	Иначе
		Возврат ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено,
			"Справочник._ДемоУдалитьБанковскиеСчета");
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли