
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Владелец") Тогда
		
		НовыйЭлемент = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		НовыйЭлемент.ИдентификаторПользовательскойНастройки = "ОтборПоВладельцу";
		НовыйЭлемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВладелецФайла");
		НовыйЭлемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		НовыйЭлемент.ПравоеЗначение = Параметры.Владелец;
		НовыйЭлемент.Использование = Истина;
		
		ВладелецФайла = Параметры.Владелец;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Оповестить("НоваяКартинка", Значение, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	
	Оповещение = Новый ОписаниеОповещения("СохранитьФайл", ЭтотОбъект);
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ВыборФайла.МножественныйВыбор = Ложь;
	ВыборФайла.Заголовок = НСтр("ru = 'Выбор файла'");
	ВыборФайла.Фильтр = НСтр("ru = 'Все файлы (*.*)|*.*'");
	
	НачатьПомещениеФайлов(Оповещение, , ВыборФайла, , УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьФайл(АдресВременногоХранилищаФайла, ИмяФайла)
	
	СтруктураФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ИмяФайла);
	Файл = Справочники.НоменклатураПрисоединенныеФайлы.СоздатьЭлемент();
	Файл.Наименование = СтруктураФайла.Имя;
	Файл.ВладелецФайла = ВладелецФайла;
	Если Не ПустаяСтрока(АдресВременногоХранилищаФайла) Тогда
		ФайлХранилище = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаФайла);
		Файл.ФайлХранилище = Новый ХранилищеЗначения(ФайлХранилище);
	КонецЕсли;
	
	Файл.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайл(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Количество() Тогда
		СоздатьФайл(Результат[0].Хранение, Результат[0].Имя);
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
