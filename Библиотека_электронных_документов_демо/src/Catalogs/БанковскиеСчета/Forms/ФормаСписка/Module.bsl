#Область ОписаниеПеременных

&НаКлиенте
Перем УстановкаОсновногоБанковскогоСчетаВыполнена;

#КонецОбласти

#Область ОбработчикиСобытийФорм

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат; // Возврат при получении формы для анализа.
	КонецЕсли;
	
	Параметры.Отбор.Свойство("Владелец", Владелец);
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		// Контекстное открытие формы с отбором по контрагенту/организации
		
		ЭтоКонтрагент = ТипЗнч(Владелец) = Тип("СправочникСсылка.Контрагенты");
		
		ДоступноИспользоватьОсновным = ПравоДоступа("Редактирование",
			?(ЭтоКонтрагент, Метаданные.Справочники.Контрагенты, Метаданные.Справочники.Организации));
			
		Элементы.ИспользоватьКакОсновной.Видимость = ДоступноИспользоватьОсновным;
		
		ОсновнойБанковскийСчет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ОсновнойБанковскийСчет");
		Список.Параметры.УстановитьЗначениеПараметра("ОсновнойБанковскийСчет", ОсновнойБанковскийСчет);
			
	Иначе
		// Открытие в общем режиме
	
		Элементы.Владелец.Видимость = Истина;
		ДоступноИспользоватьОсновным = ПравоДоступа("Редактирование", Метаданные.Справочники.Контрагенты)
			И ПравоДоступа("Редактирование", Метаданные.Справочники.Организации);
		
		Элементы.ИспользоватьКакОсновной.Видимость = ДоступноИспользоватьОсновным;
		
		Список.Параметры.УстановитьЗначениеПараметра("ОсновнойБанковскийСчет", Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УстановкаОсновногоБанковскогоСчетаВыполнена" Тогда
		
		УстановкаОсновногоБанковскогоСчетаВыполнена = Истина;
		
	ИначеЕсли ИмяСобытия = "УстановкаОсновногоБанковскогоСчетаПриЗаписи" Тогда
		
		Если ЗначениеЗаполнено(Владелец) Тогда
		
			Если Владелец = Параметр.Владелец Тогда 
				
				Если УстановкаОсновногоБанковскогоСчетаВыполнена = Истина Тогда

					ОсновнойБанковскийСчет = Параметр.ОсновнойБанковскийСчет;
					Список.Параметры.УстановитьЗначениеПараметра("ОсновнойБанковскийСчет", ОсновнойБанковскийСчет);
					
					УправлениеФормойКлиент();
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьКакОсновной(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Список.ТекущиеДанные.Ссылка = ОсновнойБанковскийСчет Тогда
		ОсновнойБанковскийСчет = ПредопределенноеЗначение("Справочник.БанковскиеСчета.ПустаяСсылка");
	Иначе
		ОсновнойБанковскийСчет = Элементы.Список.ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ОсновнойБанковскийСчет", ОсновнойБанковскийСчет);
	
	УстановкаОсновногоБанковскогоСчетаВыполнена = Ложь;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("КонтрагентОрганизация", Элементы.Список.ТекущиеДанные.Владелец);
	СтруктураПараметров.Вставить("ОсновнойБанковскийСчет", ОсновнойБанковскийСчет);
	
	Оповестить("УстановкаОсновногоБанковскогоСчета", СтруктураПараметров);
	
	// Если форма владельца закрыта, то запишем основной банковский счет самостоятельно.
	Если НЕ УстановкаОсновногоБанковскогоСчетаВыполнена Тогда
		УстановитьОсновнойБанковскийСчет(СтруктураПараметров);
	КонецЕсли;
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеФормойКлиент()
	
	Если ОсновнойБанковскийСчет = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДоступноИспользоватьОсновным И НЕ Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Элементы.ИспользоватьКакОсновной.Пометка =
			Элементы.Список.ТекущиеДанные.Ссылка = ОсновнойБанковскийСчет;
	КонецЕсли;
	
	Если ДоступноИспользоватьОсновным Тогда
		Элементы.ИспользоватьКакОсновной.Доступность = НЕ Элементы.Список.ТекущиеДанные = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьОсновнойБанковскийСчет(СтруктураПараметров)
	
	Справочники.БанковскиеСчета.УстановитьОсновнойБанковскийСчет(
		СтруктураПараметров.КонтрагентОрганизация, 
		СтруктураПараметров.ОсновнойБанковскийСчет);
	
КонецПроцедуры

#КонецОбласти
