
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НоменклатураПоставщика.Параметры.УстановитьЗначениеПараметра("Контрагент", Параметры.Контрагент);
	Элементы.Список.ТекущаяСтрока = Параметры.Номенклатура;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	НоменклатураПоставщика.Параметры.УстановитьЗначениеПараметра("Номенклатура", Элемент.ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	Если Элементы.ПоискПоШтрихкоду.Пометка Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Ссылка");
		Элементы.ПоискПоШтрихкоду.Пометка = Ложь;
		Элементы.СписокКонтекстноеМенюПоискПоШтрихкоду.Пометка = Ложь;
	Иначе
		Оповещение = Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ЭтотОбъект);
		ПоказатьВводСтроки(Оповещение, "", НСтр("ru = 'Введите штрихкод'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(ШтрихКод, ДополнительныеПараметры) Экспорт
	
	Если ШтрихКод = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоменклатураПоШтрихкоду = НайтиНоменклатуруПоШтрихкоду(ШтрихКод);
	
	Если НоменклатураПоШтрихкоду = Неопределено Тогда
		
		// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
		Если РаботаСНоменклатуройКлиент.ДоступенФункционалПодсистемы() Тогда
			РаботаСНоменклатуройКлиент.НайтиНоменклатуруПоШтрихкодуВСервисе(ШтрихКод);
		Иначе
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Номенклатура не найдена'"),,
				СтрШаблон(НСтр("ru = 'По штрихкоду %1 номенклатура не найдена.'"), ШтрихКод),,
				СтатусОповещенияПользователя.Информация);
		КонецЕсли;
		// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Ссылка", НоменклатураПоШтрихкоду, ВидСравненияКомпоновкиДанных.ВСписке);
		Элементы.ПоискПоШтрихкоду.Пометка = Истина;
		Элементы.СписокКонтекстноеМенюПоискПоШтрихкоду.Пометка = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиНоменклатуруПоШтрихкоду(ШтрихКод)
	
	Возврат РегистрыСведений.Штрихкоды.НайтиНоменклатуруПоШтрихкоду(ШтрихКод);
	
КонецФункции

#КонецОбласти