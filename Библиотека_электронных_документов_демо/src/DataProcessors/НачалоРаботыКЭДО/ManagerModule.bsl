#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПодключитьИнтернетПоддержку(ПараметрыЗадания, АдресХранилища) Экспорт
	
	ДанныеАутентификации = Новый Структура("Логин, Пароль", ПараметрыЗадания.ЛогинИПП, ПараметрыЗадания.ПарольИПП);
	ИнтернетПоддержкаПользователей.СохранитьДанныеАутентификации(ДанныеАутентификации);
	
	Результат = "ПодключениеУспешно";
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		РезультатПолученияТикета = ИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки("https://api.orgregister.1c.ru/orgregister/");
		УстановитьПривилегированныйРежим(Ложь);
		Если НЕ ЗначениеЗаполнено(РезультатПолученияТикета.КодОшибки) Тогда
			// Проверить подключение к 1С-Контрагент
			
			НайденныеРеквизиты = РаботаСКонтрагентами.РеквизитыЮридическихЛицПоНаименованию("ТестовыйКонтрагент");
			Если ЗначениеЗаполнено(НайденныеРеквизиты.ОписаниеОшибки) Тогда
				Если ВРЕГ(НайденныеРеквизиты.ОписаниеОшибки) = ВРЕГ("НеУказаныПараметрыАутентификации")
					ИЛИ ВРЕГ(НайденныеРеквизиты.ОписаниеОшибки) = ВРЕГ("НеУказанПароль") Тогда
					Результат = "ОшибкаАвторизации";
				ИначеЕсли ВРЕГ(НайденныеРеквизиты.ОписаниеОшибки) = ВРЕГ("Сервис1СКонтрагентНеПодключен") Тогда
					Результат = "Сервис1СКонтрагентНеПодключен";
				Иначе
					Результат = "Неопределено";
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если ВРЕГ(РезультатПолученияТикета.КодОшибки) = ВРЕГ("НеверныйЛогинИлиПароль") Тогда
				Результат = "ОшибкаАвторизации";
			ИначеЕсли ВРЕГ(РезультатПолученияТикета.КодОшибки) = ВРЕГ("ОшибкаПодключения") Тогда
				Результат = "ОшибкаПодключения";
			ИначеЕсли ВРЕГ(РезультатПолученияТикета.КодОшибки) = ВРЕГ("ОшибкаСервиса") Тогда
				Результат = "ОшибкаСервиса";
			Иначе
				Результат = "Неопределено";
			КонецЕсли;
		КонецЕсли;
		
	Исключение
		Результат = "Неопределено";
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
	КонецПопытки;
	
	Если Результат <> "ПодключениеУспешно"
		И Результат <> "Сервис1СКонтрагентНеПодключен" Тогда
		// Отключаем интернет-поддержку
		ИнтернетПоддержкаПользователей.СохранитьДанныеАутентификации(Неопределено);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СобытиеЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Интернет-поддержка пользователей'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
КонецФункции

#КонецОбласти

#КонецЕсли